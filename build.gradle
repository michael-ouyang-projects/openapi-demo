plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version '1.1.0'
	id "org.openapi.generator" version "6.6.0"
}

group = 'fun.mouyang'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// spring
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// openapi
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
//	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

openApiGenerate {
	generatorName = 'spring'
	inputSpec = file("${projectDir}/src/main/resources/openapi/user.yaml").toString()
	outputDir = file("${buildDir}/generated/openapi").toString()
	apiPackage = 'fun.mouyang.interfaces.rest.controller'
	modelPackage = 'fun.mouyang.interfaces.rest.dto'
	configOptions = [
			useSpringBoot3: "true",
			interfaceOnly: "true"
	]
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir "${buildDir}/generated/openapi/src/main/java"
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

compileJava.dependsOn tasks.openApiGenerate